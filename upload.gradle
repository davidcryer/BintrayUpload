apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging packagingFormat
                groupId groupPath
                artifactId artifactName
                name libraryName
                description libraryDescription
                url gitWebsiteUrl
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url gitWebsiteUrl
                }
            }
        }
    }
}

if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

version = versionTag
group = groupPath

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = mavenRepoName
        name = artifactName
        desc = libraryDescription
        websiteUrl = gitWebsiteUrl
        vcsUrl = gitUrl
        licenses = licenseArray
        publish = true
        publicDownloadNumbers = true
        version {
            name = versionTag
            desc = versionDescription
            released = new Date()
            vcsTag = versionTag
            desc = description
        }
    }
}
